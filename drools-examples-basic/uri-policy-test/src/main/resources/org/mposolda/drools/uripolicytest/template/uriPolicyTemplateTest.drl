template header
PRIORITY
CHECK_PRIORITY
URI_PATTERN
QUERY_PARAMS_CONDITIONS
ALLOWED_REALM_ROLES
DENIED_REALM_ROLES
ALLOWED_APPLICATION_ROLES
DENIED_APPLICATION_ROLES
ALLOWED_USERS
DENIED_USERS

package org.mposolda.drools.uripolicytest;

import org.drools.WorkingMemory;
import org.mposolda.drools.uripolicytest.Result;
import org.mposolda.drools.uripolicytest.Decision;
import org.mposolda.drools.uripolicytest.UriPolicyInput;
import org.mposolda.drools.uripolicytest.Token;
import org.mposolda.drools.uripolicytest.MatcherInfo;
import org.mposolda.drools.uripolicytest.RolesContainer
import java.util.List;
import java.util.Arrays;
import java.util.ArrayList;
import java.util.regex.Pattern;
import java.util.regex.Matcher;

dialect "mvel"

template "Uri policy"
rule "uri_policy_@{row.rowNumber}"
salience @{PRIORITY}
when
  $result: Result( isAlreadyProcessedRule("uri_policy_@{row.rowNumber}") == false )
  $endChecker: EndChecker( finished == false )
  $token: Token()
  MatcherCache($uriGroup: matcherInfo("uri_policy_@{row.rowNumber}"));
  $upi: UriPolicyInput(matches(@{URI_PATTERN}, uri, $uriGroup) && (@{QUERY_PARAMS_CONDITIONS}))
then
  System.out.println("uri_policy_@{row.rowNumber}");
  System.out.println("Group 0: " + $uriGroup.group(0));
  System.out.println("Group 1: " + $uriGroup.group(1));
  System.out.println("Token: " + $token);

  RolesContainer container = new RolesContainer();
  container.addAllAllowedRealmRoles( Arrays.asList(new String[] { @{ALLOWED_REALM_ROLES} }) );
  container.addAllDeniedRealmRoles( Arrays.asList(new String[] { @{DENIED_REALM_ROLES} }) );
  container.addAllAllowedApplicationRoles( Arrays.asList(new String[] { @{ALLOWED_APPLICATION_ROLES} }) );
  container.addAllDeniedApplicationRoles( Arrays.asList(new String[] { @{DENIED_APPLICATION_ROLES} }) );
  container.addAllAllowedUsers( Arrays.asList(new String[] { @{ALLOWED_USERS} }) );
  container.addAllDeniedUsers( Arrays.asList(new String[] { @{DENIED_USERS} }) );

  Decision dr = container.isTokenAllowed($token);

  modify ($result) {
    mergeDecision(dr);
  }
  modify ($result) {
    addProcessedRule("uri_policy_@{row.rowNumber}");
  }
  modify ($result) {
      lastProcessedPriority = @{PRIORITY}
  }
end
end template

template "Check finished"
rule "check_finished_@{row.rowNumber}"
salience @{CHECK_PRIORITY}
when
  Result(decision == Decision.ACCEPT || == Decision.REJECT, lastProcessedPriority > @{CHECK_PRIORITY})
  $endChecker: EndChecker( finished == false )
then
  System.out.println("check_finished_@{row.rowNumber}");
  modify ($endChecker) {
    finished = true;
  }
end
end template
